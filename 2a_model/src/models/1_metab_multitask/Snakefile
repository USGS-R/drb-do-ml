workdir: "1a_model/src/models/2_metab_multitask"
configfile: "config.yml"

import sys
sys.path.append("../0_baseline_LSTM")

from model import LSTMModel


out_dir = os.path.join(config['out_dir'], config['exp_name'])

rule all:
    input:
          expand("{outdir}/exp_{metric_type}_metrics.csv",
                  outdir=out_dir,
                  metric_type=['overall', 'reach']),
          expand("{outdir}/nstates_{nstates}/nep_{epochs}/rep_{rep}/plots/ts_{site_id}_{year}.png",
                 outdir=out_dir,
                 nstates=config['hidden_size'],
                 epochs=config['epochs'],
                 rep=list(range(config['num_replicates'])),
                 site_id=['01480870'],
                 year=[2012])
        

module base_workflow:
    snakefile: "../Snakefile_base.smk"
    config: config


use rule * from base_workflow as base_*


use rule train from base_workflow as base_train with:
    input:
        "../../../out/models/1_lstm_metab/prepped.npz"
    params: model = lambda wildcards: LSTMModel(int(wildcards.nstates), recurrent_dropout=config['recurrent_dropout'], dropout=config['dropout'], num_tasks=len(config['y_vars']))

use rule make_predictions from base_workflow as base_make_predictions with:
    input:
        "../../../out/models/1_lstm_metab/prepped.npz",
        "{outdir}/nstates_{nstates}/nep_{epochs}/rep_{rep}/train_weights/",
        "../../../out/well_obs_inputs.zarr",
    params: model = lambda wildcards: LSTMModel(int(wildcards.nstates), recurrent_dropout=config['recurrent_dropout'], dropout=config['dropout'], num_tasks=len(config['y_vars']))
